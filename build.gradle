plugins {
    id 'fabric-loom' version '1.12.5'
    id 'maven-publish'
    id 'dev.lukebemish.opensesame.loom' version '0.8.0'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/kr1v-utils.accesswidener")
    splitEnvironmentSourceSets()

    mods {
        "kr1v-utils" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

opensesame.apply(sourceSets.main)
opensesame.apply(sourceSets.client)


repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    mavenCentral()
}

sourceSets {
    ap {
        java { srcDir "src/ap/java" }
        resources { srcDir "src/ap/resources" }
    }
}

configurations {
    apImplementation
    apAnnotationProcessor

    apCompileClasspath {
        canBeResolved = true
        extendsFrom apImplementation
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modApi "com.github.sakura-ryoko:malilib:${project.malilib_version}"

    modImplementation include("dev.lukebemish.opensesame:opensesame-fabric:${project.opensesame_version}")
    implementation include("net.fabricmc:tiny-remapper:${project.tiny_version}")
    implementation include("net.fabricmc:mapping-io:${project.mapping_io_version}")
    implementation include("org.reflections:reflections:${project.reflections_version}")
    implementation include("org.javassist:javassist:${project.javaasist_version}-GA")

    apImplementation "com.google.auto.service:auto-service-annotations:1.1.1"
    apImplementation "com.google.code.gson:gson:2.13.2"
    apAnnotationProcessor "com.google.auto.service:auto-service:1.1.1"
}

def targetJavaVersion = 21
def apGeneratedDir = file("$buildDir/generated/sources/annotations/main")

afterEvaluate {
    if (configurations.named("apCompileClasspath") && configurations.named("apAnnotationProcessor")) {
        configurations.apCompileClasspath.extendsFrom(configurations.apAnnotationProcessor)
    }
}

if (tasks.names.contains('compileApJava')) {
    tasks.named('compileApJava', JavaCompile).configure { JavaCompile jc ->
        jc.group = "build"
        jc.description = "Compile annotation processor"
        jc.source = sourceSets.ap.java
        jc.classpath = files(sourceSets.ap.compileClasspath, configurations.apCompileClasspath)
        jc.destinationDirectory = file("$buildDir/classes/java/ap")
        jc.options.encoding = "UTF-8"
        jc.options.annotationProcessorPath = configurations.apAnnotationProcessor
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            jc.options.release.set(targetJavaVersion)
        } else {
            jc.options.compilerArgs += ["--release", "${targetJavaVersion}"]
        }
    }
} else {
    tasks.register('compileApJava', JavaCompile) { JavaCompile jc ->
        jc.group = "build"
        jc.description = "Compile annotation processor"
        jc.source = sourceSets.ap.java
        jc.classpath = files(sourceSets.ap.compileClasspath, configurations.apCompileClasspath)
        jc.destinationDirectory = file("$buildDir/classes/java/ap")
        jc.options.encoding = "UTF-8"
        jc.options.annotationProcessorPath = configurations.apAnnotationProcessor
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            jc.options.release.set(targetJavaVersion)
        }
    }
}

sourceSets.main.java.srcDir apGeneratedDir

tasks.withType(JavaCompile).configureEach { JavaCompile jc ->
    if (jc.name == 'compileApJava') return

    dependsOn tasks.named('compileApJava')

    jc.options.annotationProcessorPath = files(layout.buildDirectory.dir("classes/java/ap"), configurations.apCompileClasspath)

    try {
        jc.options.annotationProcessorGeneratedSourcesDirectory = apGeneratedDir
    } catch (MissingPropertyException ignored) {
        jc.options.compilerArgs += ["-s", apGeneratedDir.absolutePath]
    }

    jc.doFirst { apGeneratedDir.mkdirs() }
}

tasks.named("remapJar") {
    dependsOn tasks.named("openSesameCompileClientJava")
}

tasks.named("jar") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn tasks.named("openSesameCompileClientJava")
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

tasks.named("sourcesJar") {
    dependsOn tasks.named("compileJava")
    dependsOn tasks.named("compileClientJava")
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
    // retrieving dependencies.

    repositories {
    }
}
